# Exports
setenv SHELL            /bin/tcsh
setenv EDITOR           gvim
setenv RCFILE           $HOME/.cshrc
setenv TERM             xterm
setenv PATH             /sbin:$HOME/bin:$PATH

# For github
setenv GITHUB           $HOME/github
setenv PATH             $GITHUB/main/bin:$PATH
setenv PYTHONPATH       $GITHUB/pylib:$PYTHONPATH
setenv RCCMNFILE        $GITHUB/rcfiles/.cshrc.common

# Allow group permission
umask 007

# Disabling autocorrection and listmax prompt questions
set correct             = command
set listmax             = 10000
set listmaxrows         = 1000

# Command history remembered by the shell
set history             = 1000
set savehistory         = (2000 merge)

# Command prompt
if ($HOME == "/root") then
    set prompt              = "$prompt\[%T `hostname -s` %c]# "
else
    set prompt              = "$prompt\[%T `hostname -s` %c]% "
endif

# Common aliases
alias rc                'source $HOME/.cshrc'
alias e                 '$EDITOR'
alias erc               '$EDITOR $RCFILE'
alias ercc              '$EDITOR $RCCMNFILE'
alias ee                '(\!*) > /tmp/ee; $EDITOR /tmp/ee'
alias ew                'which \!*; $EDITOR `which \!*`'
alias ls                '/bin/ls -a --color=auto'
alias cp                '/bin/cp -rf'
alias k                 '/bin/kill -9'
alias masskill          'ps | grep \!:1 | cut -f2 -d " " | xargs /bin/kill -9'
alias q                 'exit'
alias qqqqqq            'ps -U $USER -o pid= | xargs /bin/kill -9'
alias psmy              'ps -ef | grep $USER'
alias perf              'time /bin/ls . >/dev/null'
alias path              'readlink -f \!*'
alias fixpath           'cd `path .`'
alias x                 '(xterm -T `pwd` -e "\!*; csh" &)'
alias hist              'history | grep "\!*" | grep -v hist'
alias rdiff             'tkdiff \!:1 \!:2/\!:1 &'
alias rndpath           'pwd | md5sum | cut -f1 -d" "'
alias fixme             'grep "F\IXME" * -rs'
alias fixmeme           'fixme | grep $USER'
alias grepr             'grep \!* * -r'
alias mailme            '& tee /tmp/`rndpath`; (echo $PWD \\n\\n`history -h 1`\\n\\n; cat /tmp/`rndpath`) | mail -s "Job CMD=`history -h 1` is done." $USER'
alias replace           'find . -type f -exec sed -i "s/\!:1/\!:2/g" {} +'
alias xrepeat           '(xterm -T "xrepeat \!*" -e "watch \!*; csh" &)'
alias tabname           'qdbus org.kde.konsole $KONSOLE_DBUS_SESSION setTitle 1 "\!:*" >> /dev/null'

